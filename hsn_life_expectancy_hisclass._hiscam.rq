#+ summary: correlate hisclass/ hiscam of hsn persons with their life expectancy, including age at occupational observation(s)
#+ tags:
#+      - HSN
#+      - hisclass
#+      - life expectancy
#+      - hiscam


PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX hsndim: <https://iisg.amsterdam/hsn/dimension/>
PREFIX hsn: <https://iisg.amsterdam/hsn/>
PREFIX hsn-hisco: <https://iisg.amsterdam/hsn2013a/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX sdmxdim: <http://purl.org/linked-data/sdmx/2009/dimension#>
prefix relacode: <https://iisg.amsterdam/hsn/code/RELACODE/>
prefix hsnvocab: <https://iisg.amsterdam/hsn2013a/vocab/>
prefix volgnummer: <https://iisg.amsterdam/hsn/code/INGVLGNR/>


SELECT DISTINCT ?idnr ?ovldat ?gebjaar ?hisclass ?hiscam ?occ ?occyear  ?ovlyear 
(bif:datediff('year', xsd:date(str(?gebdat)),xsd:date(str(?ovldat))) AS ?age_at_death )
(bif:datediff('year', xsd:date(str(?gebdat)),xsd:date(str(?occdat))) AS ?age_at_occ)

WHERE{
  #  ?person a hsn:OP .
    ?person hsndim:individualObservation ?life .
    ?life hsndim:GEBJAR ?gebjaar .    
    ?life hsndim:GEBDAT ?gebdat .
    ?life hsndim:OVLDAT ?ovldat .
    ?life hsndim:OVLJAR ?ovlyear .
    ?life hsndim:IDNR ?idnr .

  #  ?person a hsn:OP .
    ?person hsndim:individualObservation ?beroep .
    ?beroep hsndim:occupationalTitle ?occ .  
    ?beroep hsndim:INGDAT ?occdat .
    ?beroep hsndim:INGJAR ?occyear .
    ?occ hsn-hisco:hisclass ?hisclass .
    ?hisco skos:member ?occ .
    #?hisco skos:inScheme ?scheme .
    ?hisco <https://iisg.amsterdam/hiscam/v131/nl> ?hiscam .
  
### FILTER for difference between occupational year and year of death
 FILTER(bif:datediff('year', xsd:date(str(?occdat)),xsd:date(str(?ovldat))) < 20)
   
### FILTER for age at occupational observation
  FILTER(bif:datediff('year', xsd:date(str(?gebdat)),xsd:date(str(?occdat))) > 40)
  
### FILTER out unlikely matches where occupational year exceeds year of death
  FILTER(bif:datediff('year', xsd:date(str(?gebdat)),xsd:date(str(?ovldat))) > bif:datediff('year', 			 xsd:date(str(?gebdat)),xsd:date(str(?occdat))))
}
